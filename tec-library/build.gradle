plugins {
    id 'java'
    id 'jacoco'
    id 'com.jaredsburrows.license' version '0.9.7'
}


java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}
group = 'net.heretical_camelid.transit_emv_checker.library'
version = rootProject.ext.versionName

dependencies {
    implementation "com.github.devnied.emvnfccard:library:3.0.1"
    implementation "com.github.devnied:bit-lib4j:1.5.2"
    implementation "org.apache.commons:commons-lang3:3.8.1"
    implementation 'org.apache.commons:commons-collections4:4.3'
    implementation 'commons-io:commons-io:2.13.0'
    implementation "net.sf.scuba:scuba-smartcards:0.0.18"
    implementation "org.slf4j:slf4j-api:2.0.13"
    testImplementation "org.slf4j:slf4j-reload4j:2.0.13"
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.0'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.0'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    // The following line ensures that a report is generated
    // listing dependency copyrights and licenses
    implementation 'com.jaredsburrows:gradle-license-plugin:0.9.7'
}

test {
    useJUnitPlatform {
        includeTags "run_with_gradle"
    }
    testLogging {
        events "passed", "skipped", "failed"
    }
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn  test
}

// Capability to build a 'fat' JAR following:
// https://docs.gradle.org/current/userguide/building_java_projects.html#sec:java_packaging
tasks.register('uberJar', Jar) {
    archiveClassifier = 'uber'
    duplicatesStrategy = 'include'
    from sourceSets.main.output
    dependsOn configurations.runtimeClasspath
    from {
        configurations.runtimeClasspath.findAll { it.name.endsWith('jar') }.collect { zipTree(it) }
    }
}

licenseReport {
    generateHtmlReport = true
    generateCsvReport = false
    generateJsonReport = false
    generateTextReport = false
    copyHtmlReportToAssets = true
    useVariantSpecificAssetDirs = false
}

