plugins {
    id 'com.android.application'
    id 'base'
    id 'com.jaredsburrows.license' version '0.9.7'
    id 'com.google.gms.google-services'
}

// Following recipe for keeping key secrets out of git
// https://developer.android.com/studio/publish/app-signing#secure-shared-keystore
// The filename in the next line will be rewritten for signed builds
def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file("replace_with_keystore_properties_path")
if(keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    namespace 'net.heretical_camelid.transit_emv_checker.android_app'
    compileSdk 34

    defaultConfig {
        applicationId "net.heretical_camelid.transit_emv_checker.android_app"
        minSdk 25
        targetSdk 34
        // Google Play store requires versionCode for releases to be
        // monotonically increasing
        // as I had already used versionCode 235097892 before I found
        // this out, the pattern is now
        // prefix 30
        // 2 digits each for for major, minor, patch version number
        // 1 trailing digit starts at zero can be updated for successive
        // attempts within the same release number
        versionCode 300001030
        // CI script update_build.sh will substitute the 7 hex digit
        // prefix of the current git hash in place of -dirty in the
        // line below.
        // For non-CI builds, the -dirty suffix will be replaced by
        // -devbuild-{timestamp} where the version name appears
        // in the about page.
        versionName "0.1.3-dirty"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        release {
            if(keystorePropertiesFile.exists()) {
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            if(keystorePropertiesFile.exists()) {
                signingConfig signingConfigs.release
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    buildFeatures {
        viewBinding true
        buildConfig true
    }
    lint {
        disable += [ 'DefaultLocale' ]
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
        managedDevices {
            localDevices {
                defaultGoogleATD /*(com.android.build.api.dsl.ManagedVirtualDevice)*/ {
                    // A lower resolution device is used here for better emulator performance
                    device = "Nexus One"
                    apiLevel = 34
                    // The 'google' image is required because 'aosp-atd' does not contain
                    // the system file picker
                    systemImageSource = "google"
                    //abi = "x86_64"
                }
            }
        }
    }
}

base {
    archivesName = "TransitEMVChecker-$android.defaultConfig.versionName"
}

dependencies {
    implementation project(':tec-library')

    implementation "com.github.devnied.emvnfccard:library:3.0.1"
    implementation "com.github.devnied:bit-lib4j:1.5.2"
    implementation "org.apache.commons:commons-lang3:3.8.1"
    implementation "org.apache.commons:commons-collections4:4.3"
    implementation "commons-io:commons-io:2.13.0"
    implementation "net.sf.scuba:scuba-smartcards:0.0.18"

    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'

    // Can update 2.7.0 to 2.8.0 for this block of components
    // when navigation-fragment and navigation-ui have a released
    // 2.8.0
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.7.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.7.0'
    implementation 'androidx.navigation:navigation-fragment:2.7.7'
    implementation 'androidx.navigation:navigation-ui:2.7.7'

    // The following line ensures that a report is generated
    // listing dependency copyrights and licenses
    implementation 'com.jaredsburrows:gradle-license-plugin:0.9.7'

    implementation "org.slf4j:slf4j-simple:2.0.13"
    implementation 'androidx.test.espresso:espresso-accessibility:3.5.1'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestImplementation 'androidx.test:rules:1.5.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation('androidx.test.uiautomator:uiautomator:2.3.0')

    implementation platform('com.google.firebase:firebase-bom:33.1.0')
    implementation 'com.google.firebase:firebase-analytics'
}

licenseReport {
    generateHtmlReport = true
    generateCsvReport = false
    generateJsonReport = false
    generateTextReport = false
    copyHtmlReportToAssets = true
    useVariantSpecificAssetDirs = false
}




