plugins {
    id 'com.android.application'
    id 'base'
    id 'com.jaredsburrows.license' version '0.9.7'
    id 'com.google.gms.google-services'
}

def kspath = "$System.env.kspath"
def vname_suffix = "dirty"
def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file("$kspath")
if(keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
    vname_suffix="$System.env.githash"
}

android {
    namespace 'net.heretical_camelid.transit_emv_checker.android_app'
    compileSdk 34

    defaultConfig {
        applicationId "net.heretical_camelid.transit_emv_checker.android_app"
        minSdk 26
        targetSdk 34
        // Google Play store requires versionCode for releases to be
        // monotonically increasing
        // as I had already used versionCode 235097892 before I found
        // this out, the pattern is now
        // prefix 30
        // 2 digits each for for major, minor, patch version number
        // 1 trailing digit starts at zero can be updated for successive
        // attempts within the same release number
        versionCode 300002000
        // For builds triggered under build_and_test_everything.sh
        // where the working directory exactly matches git HEAD,
        // the 7-hex-prefix of the current git HEAD hash will
        // be substituted for the $vname_suffix variable
        // in the line below.
        // For other builds, the variable may include an
        // additional 3-hex-digit suffix derived from the
        // hash of git HEAD, or may be the string 'dirty'.

        // Builds done directly using .gradlew will use the
        // value 'dirty' in place of the hash suffix.
        versionName "0.2.x-$vname_suffix"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        release {
            if(keystorePropertiesFile.exists()) {
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            if(keystorePropertiesFile.exists()) {
                signingConfig signingConfigs.release
            }
        }
        debug {
            enableUnitTestCoverage true
            enableAndroidTestCoverage true
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
        // desugaring required to use LocalDateTime at minSdk 25
        coreLibraryDesugaringEnabled true
    }
    buildFeatures {
        viewBinding true
        buildConfig true
    }
    lint {
        disable += [ 'DefaultLocale', 'ObsoleteSdkInt', 'VectorPath', 'UnusedResources' ]
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
        managedDevices {
            localDevices {
                defaultGoogleATD /*(com.android.build.api.dsl.ManagedVirtualDevice)*/ {
                    // A lower resolution device is used here for better emulator performance
                    device = "Pixel 6"
                    apiLevel = 34
                    // The 'google' image is required because 'aosp-atd' does not contain
                    // the system file picker
                    systemImageSource = "google"
                    // abi = "x86_64"
                }
            }
        }
    }
    sourceSets {
        main {
            // Bring in the XML capture files used for automated tests
            // TODO: Work out how to include these in debug builds only
            assets.srcDirs += [ '../tec-library/src/main/assets' ]
            // Bring in the library files so we get coverage over them
            java.srcDirs += [ '../tec-library/src/main/java' ]
        }
    }
}

base {
    archivesName = "TransitEMVChecker-$android.defaultConfig.versionName"
}

dependencies {
    // desugaring required to use LocalDateTime at minSdk 25
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.1.2'

    //implementation project(':tec-library')

    implementation "com.github.devnied.emvnfccard:library:3.0.1"
    implementation "com.github.devnied:bit-lib4j:1.5.2"
    implementation "org.apache.commons:commons-lang3:3.8.1"
    implementation "org.apache.commons:commons-collections4:4.3"
    implementation "commons-io:commons-io:2.13.0"
    implementation "net.sf.scuba:scuba-smartcards:0.0.18"

    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'

    // Can update 2.7.0 to 2.8.0 for this block of components
    // when navigation-fragment and navigation-ui have a released
    // 2.8.0
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.8.6'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.8.6'
    implementation 'androidx.navigation:navigation-fragment:2.8.1'
    implementation 'androidx.navigation:navigation-ui:2.8.1'

    // The following line ensures that a report is generated
    // listing dependency copyrights and licenses
    implementation 'com.jaredsburrows:gradle-license-plugin:0.9.7'

    implementation "org.slf4j:slf4j-simple:2.0.13"
    implementation 'androidx.test.espresso:espresso-accessibility:3.6.1'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation "androidx.test:core:1.6.1"
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test:runner:1.6.1'
    androidTestImplementation 'androidx.test:rules:1.6.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
    androidTestImplementation('androidx.test.uiautomator:uiautomator:2.3.0')

    // firebase libraries don't seem to be compatible with a
    // desugared build to allow minSdk 25 to be supported.
    // TBD decide whether to bring these back, remove the desugaring
    // and give up on minSdk 25 support
    // implementation platform('com.google.firebase:firebase-bom:33.3.0')
    // implementation 'com.google.firebase:firebase-analytics'

    // refs:
    // https://github.com/FasterXML/jackson-dataformat-xml/issues/533
    // https://stackoverflow.com/questions/30968735/using-com-bea-xml-stream-package-on-android
    // Android does not have javax.xml by default, but can
    // bring in a suitable substitute like this:
    implementation 'javax.xml.stream:stax-api:1.0-2'
    implementation 'com.fasterxml:aalto-xml:1.3.1'
}

licenseReport {
    generateHtmlReport = true
    generateCsvReport = false
    generateJsonReport = false
    generateTextReport = false
    copyHtmlReportToAssets = true
    useVariantSpecificAssetDirs = false
}

project.afterEvaluate {
    tasks.withType(JacocoReport).configureEach {
        println(build.dependsOn)
        build.dependsOn.remove("connectedDebugAndroidTest")

    }
}


apply plugin: 'jacoco'
evaluationDependsOn ':tec-library'
tasks.withType(JacocoReport).configureEach {
    apply plugin: 'java-library'
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
    def otherSourceSet = project(':tec-library').sourceSets.main
    additionalSourceDirs = otherSourceSet.allJava
    additionalClassDirs = otherSourceSet.output
    //additionalSourceDirs.from otherSourceSet.allJava
    //additionalClassDirs.from otherSourceSet.output
}




